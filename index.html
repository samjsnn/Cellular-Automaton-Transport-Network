<!DOCTYPE HTML>
<html>
  <head>
    <link href = 'https://fonts.googleapis.com/css?family=Open+Sans:400,700' rel = 'stylesheet' type = 'text/css'>
    <style>
      html, body {
	  margin: 0; padding: 0;
	  background-color: #02040e;
	  color: #5f9182;

	  font-family: 'open sans', sans-serif;
	  font-size: 14px;
      }
      h2 {
	  font-size: 18px;
	  color: #6b8;
      }

      /*UI*/

      #container {
	  width: 720px;
	  margin: 4px auto;
      }
      #credits{
	  margin-top: 16px;
	  font-size: 12px;
      }

      /* WORLD*/

      #world {
	  border: 1px #ccc none;/*dashed;*/

	  margin: 4px auto;

	  position: relative;

	  width: 720px;
	  height: 720px;

	  /*overflow: hidden;*/
      }

    </style>

    <script src = "../shared/gl2.js"></script>
    
    <!-- quad vertices-->
    <script id = "v-quad" type = "text/glsl">#version 300 es
    in vec2 x; void main() { gl_Position = vec4(x, 0., 1.); }
    </script>

    <!-- rendering particles -->
    <script id = "v-p-part" type = "text/glsl">#version 300 es
    in vec2 ij;
      uniform sampler2D p;

      out float st;
      
      void main() {
	  vec4 part = texelFetch(p, ivec2(ij), 0);
	  vec2 xy = part.xy;
	  st = part.a;
	  
	  gl_PointSize = 2.;
	  gl_Position = vec4(xy*2.-1., 0., 1.);
      }
    </script>

    <script id = "f-p-part" type = "text/glsl">#version 300 es
      precision mediump float;
      
      uniform sampler2D f;

      const float deposit = 0.1;
      const float consume = 0.01;
    in float st;
      out vec4 fragc;
      
      void main () {
	  vec4 prev = texture(f, gl_FragCoord.xy/720.);

	  /* make particle location transparent for debugging. */
	  fragc = vec4(prev.rgb, 0.) + vec4(deposit, -consume, 0., 0.)*st;
      }
    </script>

    <!-- timestepping particles -->
    <script id = "f-p-data" type = "text/glsl">#version 300 es
#define PI 3.1415926536

      precision mediump float;

      const float TAU = 2.*PI;
      
      uniform sampler2D p;
      uniform sampler2D f;
      uniform float t;
      
      out vec4 fragc;

      const float v = 1./720.;

      uniform float off; /* sensory offset */
      uniform float ang; /* sensory angle */
      uniform float trn; /* turning angle */
      
      float rand(vec2 co){
	  return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * (43758.5453+t));
      }
      
      void main () {
	  vec4 part = texelFetch(p, ivec2(gl_FragCoord.xy), 0);
	  vec4 prev = texture(f, part.xy);
	  
	  vec2 xy = part.xy;
	  float th = part.z;
	  float st = part.a;

	  /* sense surroundings */
	  float ff = texture(f, xy+off*vec2(cos((th    )*TAU), sin((th    )*TAU))).r;
	  float fl = texture(f, xy+off*vec2(cos((th+ang)*TAU), sin((th+ang)*TAU))).r;
	  float fr = texture(f, xy+off*vec2(cos((th-ang)*TAU), sin((th-ang)*TAU))).r;
	  
	  th += ang*max(-1., min(1., (fl-fr)/(fl+fr+ff+1e-6)*trn));
	  
	  vec2 dxy = vec2(cos(th*TAU), sin(th*TAU))*v;
	  
	  /* update */
	  float attempt = sign(texture(f, xy+dxy).a);
	  xy = mix(xy,           fract(xy+dxy), attempt);
	  th = mix(rand(xy+dxy), fract(th),     attempt);
	  st = mix(0.,           1.,            attempt);

	  fragc = vec4(xy, th, st);
      }
    </script>

    <!-- timestepping for background field -->
    <script id = "f-f-data" type = "text/glsl">#version 300 es
      precision mediump float;

      uniform sampler2D f;
      out vec4 fragc;

      uniform float diff;
      uniform float evap;

      uniform float actn;
      uniform float diam;
      uniform vec2 curs;
            
      const vec2[4] dxy = vec2[4](
	  vec2(1., 0.), vec2(0., 1.), vec2(-1., 0.), vec2(0., -1)
      );

      void main () {
	  vec2 xy = gl_FragCoord.xy/720.;
	  vec4 prev = texture(f, xy);

	  /* na√Øve diffusion */
	  float ph = prev.r;
	  for (int i = 0; i < 4; ++i) {
	      ph += diff * (texture(f, (gl_FragCoord.xy+dxy[i])/720.).r - prev.r);
	  }
	  ph = mix(ph, 0., evap);

	  float g = prev.g; /* food source */
	  float c = float(distance(xy, curs) < diam); /* in cursor rance */

	  /* interaction: deposit food */
	  g = max(g, c * max(actn, 0.) * .02);
	  ph = ph + g;

	  /* interaction: erase food */
	  g  = mix(g,  0., max(-c*actn, 0.));
	  ph = mix(ph, 0., max(-c*actn, 0.));

	  
	  fragc = vec4(ph, g, 0., 1.);
      }
    </script>

    <!-- colour render -->
    <script id = "f-img" type = "text/glsl">#version 300 es
      precision mediump float;

      uniform sampler2D f;
      out vec4 fragc;

vec3 hsv2rgb (vec3 c)
{
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}
      
      vec4 palette (float r) {

	  r = mix(r, floor(r*8.)/8., .5);

	  return vec4(hsv2rgb(vec3(
	      .7-.55*r,
	      mix(.8, 1., r),
	      r
	  )), 1.);
      }
      
      void main () {
	  vec4 prev = texture(f, gl_FragCoord.xy/720.);
	  fragc = palette(prev.r);
      }
    </script>

  </head>
  <body>

    <div id = "container">
      <h2>EMERGENT TRANSPORT NETWORKS</h2>

      <canvas id = "world" width = "720" height = "720"></canvas>
      
      <div><p>

	  Population of agents forming transport networks guided by
	  local behaviour based on chemotaxis, a model inspired
	  by <i>Physarum polycephalum.</i> As described by
	  J. Jones (2010), <i>Characteristics of Pattern Formation and
	  Evolution in Approximations of Physarum Transport
	  Networks,</i> with some modifications.
	  <ul>
	    <li>A/S,O/P,T/Y: adjust agent parameters (sensory angle/offset, turning ratio).</li>
	    <li>D/F,E/R: adjust environment parameters (trail diffusion/evaporation).</li>
	    <li>LMB/RMB/wheel: erase/deposit food/pheromone trail</li>
	    <li>&uarr;/&darr;: decrease/increase timestep.</li>
	  </ul>
      </p></div>
      <script src = "physarum.js"></script>
      <div id = "credits">
	J. Lo, September 2019.
      </div>

    </div>

  </body>
</html>
